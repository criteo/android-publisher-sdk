apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def support_version = "27.0.1"

android {
    buildToolsVersion '27.0.3'
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

}

repositories {
    jcenter() // includes the MoPub SDK and AVID library
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
}


configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$support_version"
        force "com.android.support:support-v4:$support_version"
        force "com.android.support:asynclayoutinflater:$support_version"
        force "com.android.support:customtabs:$support_version"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.google.android.gms:play-services-ads:15.0.0'
    androidTestCompile 'org.mockito:mockito-android:2.7.22'
    implementation project(path: ':publisher-sdk')
    implementation('com.mopub:mopub-sdk-banner:5.6.0@aar') {
        transitive = true
    }

    // For interstitials
    implementation('com.mopub:mopub-sdk-interstitial:5.6.0@aar') {
        transitive = true
    }
}


task packageAarRelease(type: Zip, dependsOn: 'bundleRelease') {
    from file("${project.buildDir}/outputs/aar/mediation-release.aar")
    classifier 'release'
}

task packageAarDebug(type: Zip, dependsOn: 'bundleDebug') {
    from file("${project.buildDir}/outputs/aar/mediation-debug.aar")
    classifier 'debug'
}

publish {
    publishing {
        with rootProject.publishRepositoryConfig
        publications {
            zip(MavenPublication) {
                groupId 'com.criteo.mediation'
                artifactId 'sdk'
                version rootProject.publishVersion
                artifact packageAarRelease
                artifact packageAarDebug
            }
        }
    }
}



